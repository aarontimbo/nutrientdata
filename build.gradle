import org.gradle.api.plugins.shadow.ShadowPlugin
import org.gradle.api.plugins.shadow.transformers.ServiceFileTransformer

apply plugin: 'groovy'
//apply plugin: 'cobertura'
//apply plugin: 'codenarc'
apply plugin: 'application'
apply plugin: 'project-report'
apply plugin: 'idea'
apply plugin: ShadowPlugin


group = 'com.atimbo'
version = "${currentVersion}"

// The main class of the application
mainClassName = 'com.atimbo.fitness.nutrient.NutrientDataService'

// Use Java 6 by default
sourceCompatibility = 1.6
targetCompatibility = 1.6

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url 'http://repo.jfrog.org/artifactory/gradle-plugins'
        }
        ivy {
            url "http://dl.bintray.com/content/johnrengelman/gradle-plugins"
            layout "maven"
        }
    }
    dependencies {
        classpath "net.saliman:gradle-cobertura-plugin:1.1.0"
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:2.0.16'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.0.2'
        classpath 'org.gradle.plugins:shadow:0.7.4'
    }
}

// Run code narc before the test phase begins
//test.dependsOn codenarcMain

repositories {
    mavenLocal()
    mavenCentral()
    // Spock snapshots are available from the Sonatype OSS snapshot repository
    maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
}

dependencies {
    compile "org.codehaus.groovy:groovy-all:${groovyVersion}"

    compile "com.yammer.dropwizard:dropwizard-core:${dropwizardVersion}"
    compile "com.yammer.dropwizard:dropwizard-hibernate:${dropwizardVersion}"
    compile "com.yammer.dropwizard:dropwizard-migrations:${dropwizardVersion}"
    compile "com.yammer.dropwizard:dropwizard-client:${dropwizardVersion}"

    compile 'org.eclipse.jetty:jetty-servlets:8.1.8.v20121106'

    compile 'asm:asm-all:2.2'

    // DB
    compile 'mysql:mysql-connector-java:5.1.25'
    //compile 'org.postgresql:postgresql:9.3-1100-jdbc4'

    testCompile "com.yammer.dropwizard:dropwizard-testing:${dropwizardVersion}"
    testCompile "org.spockframework:spock-core:${spockVersion}"
    testCompile 'junit:junit:4.11'
    testCompile 'com.h2database:h2:1.3.168'
}

jar {
    manifest {
        attributes 'Main-Class': 'com.atimbo.fitness.nutrient.NutrientDataService'
        attributes 'Built-Date': new Date() //now
        attributes 'Built-By': System.getProperty('user.name')
        attributes 'Build-Jdk': System.getProperty('java.version')
        attributes 'Implementation-Title': project.name
        attributes 'Implementation-Version': project.version
        attributes 'Implementation-Vendor-Id': project.group
    }
}

//cobertura {
//    coverageFormats = ['xml', 'html']
//    coverageIncludes = ['**/*.java', '**/*.groovy']
//    coverageSourceDirs = [sourceSets.main.allSource.srcDirs]
//}
/*
codenarc {
    configFile = rootProject.file('codenarcrule.groovy')
    reportFormat = System.properties['codeNarcOutput'] ?: 'html'
}
*/

shadow {
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    transformer ServiceFileTransformer
}

task runApp(dependsOn: 'shadow', group: 'dropwizard',  description: "Run service using dev configuration") << {
    // Grab any dw. properties that are specified on the command line and pass to the Java process (i.e. dw.http.port=8080)
    def props = project.ext.properties.findAll { it.key.startsWith('dw') }
    javaexec {
        main = '-jar'
        args = ["${shadow.shadowJar.getPath()}", 'server', 'mysql_dev_config.yml']
        systemProperties = props
    }
}

task migrate(dependsOn: 'shadow', group: 'dropwizard', description: "Run migrations using the dev configuration") << {
    javaexec {
        main = '-jar'
        args = ["${shadow.shadowJar.getPath()}", 'db', 'migrate', 'mysql_dev_config.yml']
    }
}

task dropAll(dependsOn: 'shadow', group: 'dropwizard',  description: "Run drop-all database command") << {
    javaexec {
        main = '-jar'
        args = ["${shadow.shadowJar.getPath()}", 'db', 'drop-all', 'mysql_dev_config.yml', '--confirm-delete-everything']
    }
}
